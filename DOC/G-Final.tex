\documentclass{article}

\usepackage{amssymb, amsmath}
\usepackage{listings}
\usepackage{color}
\usepackage[usenames,dvipsnames,svgnames,table]{xcolor}

\lstdefinelanguage{Paladim}{
    morekeywords={program, function, procedure, var, begin, end, if, then, else, while},
    morekeywords={do, return, array, of, int, bool, char, and,  or, not, true, false},
    morecomment=[l]{//},
    morestring=[bd]',
    morestring=[bd]",
    sensitive=true,
}

\lstdefinelanguage{MIPS}{
    morekeywords={addi, add, sw, lw, j, beq, bne, slt, subi, sub, div, and, andi, or, ori},
    morecomment=[l]{\#},
    morestring=[bd]',
    morestring=[bd]",
    sensitive=false,
}

\lstdefinelanguage{SML}{
    morekeywords={ if, val, fun, let, in, end, then, else, andalso, orelse },
    morecomment=[s]{(*}{*)},
    morestring=[bd]',
    morestring=[bd]",
    sensitive=false,
}

\lstdefinelanguage{Yacc}{
    morekeywords={ if, val, fun, let, in, end, then, else, andalso, orelse },
    morecomment=[s]{(*}{*)},
    morestring=[bd]',
    morestring=[bd]",
    sensitive=false,
}

\lstset{
    breaklines     = true,
    numbers        = left,
    stepnumber     = 1,
    numberstyle=\color{black},
    showstringspaces=false,
    language=SML,
    frame=single,
    basicstyle=\footnotesize\ttfamily\color{red!80!black},
      keywordstyle=\bfseries\color{blue},
      commentstyle=\color{green!40!black},
      identifierstyle=\ttfamily\color{black},
      stringstyle=\color{yellow!65!black},
}

\lstdefinestyle{appendix}{
    breaklines     = true,
    numbers        = left,
    stepnumber     = 1,
    numberstyle=\color{black},
    showstringspaces=false,
    %language=SML,
    frame=single,
    basicstyle=\tiny\ttfamily\color{red!80!black},
      keywordstyle=\bfseries\color{blue},
      commentstyle=\color{green!40!black},
      identifierstyle=\ttfamily\color{black},
      stringstyle=\color{yellow!65!black},
}

\newcommand{\Cfile}[4][SML]{ \lstinputlisting[language=#1, style = appendix,firstline=#3,lastline=#4, firstnumber=#3]{../SRC/#2}}

\title{Paladim - Final hand-in}
\author{Ask Neve Gamby, Maya Saietz \& Christian Haase}

\begin{document}
\maketitle
\tableofcontents
\newpage
\section{Parser implementation}
We've implemented the parser in mosmlyac using the grammar given in the GroupProject.pdf
as a starting point. To make the grammar suitable for LR(1)-parsing we made some
transformations of the grammar:
\begin{enumerate}
  \item If-then-else-statements in the original grammar were ambiguous. This has been
        fixed by adding a pseudo-symbol \texttt{lower\_than\_else}, and giving it a
        lower precedence than the \texttt{TElse} token. This precedence rule is then
        used in the if-then-statement.
  \item All nonterminals of the form $X \rightarrow \alpha | X \alpha$ has been transformed
        to $X \rightarrow \alpha | \alpha X$. This has been done as SML-lists are most
        effectively constructed by using \texttt{::}.
  \item In the production $Exp \rightarrow Exp \; OP \; Exp$, $OP$ has been replaced with
        the actual operator-terminals. This was done to unhide operator precedence for
        mosmlyac.
\end{enumerate}

\subsection{Testing the parser}
Most of the testing has been done using a small test-framework that we built. The framework
first parses all the files in the \texttt{DATA} folder\footnote{We had to fix a syntax error in testTypeInference.pal -- the program didn't start with \texttt{program}}. For each file, it outputs either
\texttt{FAIL} or \texttt{OK}, depending on whether any exceptions were raised. This tells us
that all the test files are parsed without exceptions, but not that they are parsed correctly.
For that, we need some syntax tree to compare against.

However, it would be far too much work to manually type syntax trees for each test file. We do
some tests that parse some code and compare the result against a hard-coded syntax tree, but
we have only done this for a few small pieces of code.

Again, the test framework outputs either \texttt{FAIL} or \texttt{OK} for each piece of code,
depending on whether the result matched the hard-coded syntax tree or not.

To run the test framework, run the following commands (in the \texttt{SRC} folder):

\begin{verbatim}
$ make clean
$ make
$ mosml ParserTest.sml
\end{verbatim}

A nicer and easier way of testing would be to use the old LL1Parser as a reference. There are
a few problems with this, though. First, it would require that we kept an additional lexer
which used the old token type. Second, the LL1Parser can't parse everything in the Paladim
grammar -- there are contructions we wouldn't be able to test at all using this approach.

In addition to all of the above, we have verified that we can compile the Paladim binary and
use it to interpret and compile many of the programs in the \texttt{DATA} folder (all the
ones that do not need the other tasks to be completed), and that we get the correct output
from these programs.

\section{Integer multiplication and division and \texttt{or} and \texttt{not}}
Multiplication and division for integers and the missing boolean operators has been succesfully implemented in the rest of the compiler, that is for both interpretation and compilation. The first two has been implemented mostly by just imitating the implementation of addition and subtraction. E.g. in \texttt{Compiler.sml} the clauses in \texttt{compileExp} for subtraction and multiplication look very similar:
\begin{lstlisting}
    | compileExp( vtable, Minus(e1, e2, _), place ) =
        let val t1 = "minus1_" ^ newName()
            val c1 = compileExp(vtable, e1, t1)
            val t2 = "minus2_" ^ newName()
            val c2 = compileExp(vtable, e2, t2)
        in c1 @ c2 @ [Mips.SUB (place,t1,t2)]
        end

    (* Task 2: Integer multiplication and division implemented: *)

    | compileExp( vtable, Times(e1, e2, _), place ) =
        let val t1 = "times1_" ^ newName()
            val c1 = compileExp(vtable, e1, t1)
            val t2 = "times2_" ^ newName()
            val c2 = compileExp(vtable, e2, t2)
        in c1 @ c2 @ [Mips.MUL (place, t1, t2)]
        end
\end{lstlisting}
The boolean operator \texttt{or} was implemented similarly to \texttt{and}. The only difference in \texttt{Compiler.sml} is that we only have to evaluate the second expression if the first expression
evaluates to false, whereas for \texttt{and} we only evaluate the second expression if the first
evaluates to true. This was simply done by branching (and thus skipping the second evaluation),
when the first evaluates to 1 (i.e. $\neq 0$) instead of 0:
\begin{lstlisting}
    | compileExp( vtable, Or(e1, e2, pos), place ) =
        let val t1 = "or1_" ^ newName()
            val c1 = compileExp(vtable, e1, t1)
            val t2 = "or2_" ^ newName()
            val c2 = compileExp(vtable, e2, t2)
            val lO = "_or_" ^ newName()
        in c1 (* do first part, skip 2nd part if already true *)
           @ [Mips.MOVE (place,t1), Mips.BNE (place, "0", lO) ]
           @ c2 (* when here, t1 was  false, so the result is t2 *)
           @ [Mips.MOVE (place,t2), Mips.LABEL lO ]
        end
\end{lstlisting}
\texttt{not} was implemented in \texttt{Compiler.sml} by simply XOR'ing the boolean value with 1, as \texttt{not x = x XOR 1}:
\begin{lstlisting}
    | compileExp( vtable, Not(e1, pos), place ) =
        let val t1 = "not_" ^ newName()
            val c1 = compileExp(vtable, e1, t1)
        in c1 @ [Mips.XORI (place, t1, "1")]
        end
\end{lstlisting}
Interpretation of the boolean operators was done by adding functions \texttt{evalOr} and \texttt{evalNot} equivalent to the existing \texttt{evalAnd}:
\begin{lstlisting}
fun evalOr (BVal (Log b1), BVal (Log b2), pos) = BVal (Log (b1 orelse b2))
  | evalOr (v1, v2, pos) =
        raise Error( "Or: argument types do not match. Arg1: " ^
                      pp_val v1 ^ ", arg2: " ^ pp_val v2, pos )

fun evalNot (BVal (Log b1), pos) = BVal (Log (not b1))
  | evalNot (v1, pos) =
        raise Error( "Not: argument: " ^ pp_val v1 ^ " must be boolean", pos )
\end{lstlisting}
and filling out the missing clauses in evalExp.
Interpretation of multiplication and division was done equivalent to that of addition and subtraction. First evaluate nested expressions and then let \texttt{evalBinop} do the actual evaluation of the relevant operation.
Type-checking of multiplication, division and \texttt{or} was done by using the code for type-checking addition and \texttt{and}. Type-checking \texttt{not}-expressions was only a little bit different from type-chekcing \texttt{and} and \texttt{or} as it takes only one argument:
\begin{lstlisting}
    | typeCheckExp ( vtab, AbSyn.Not (e,    pos), _ ) =
        let val e_new = typeCheckExp(vtab, e, UnknownType )
            val e_type= typeOfExp e_new
        in  if  typesEqual(BType Bool, e_type)
            then Not(e_new, pos)
            else raise Error("in type check and exp, one argument is not of bool type "^
                             pp_type e_type  ^ " at ", pos)
        end
\end{lstlisting}

\subsection{Testing the new operators}
The boolean operators were mainly tested by running \texttt{logical.pal}, although additional
test-programs was written. When run they all print the expected result and the actual result,
so that it is easy to check if the program has been interpreted or compiled correctly.
Multiplication and division was tested using \texttt{testMulDiv.pal}, although we also tested
the behaviour for overflow and zero division (using the corresponding paladim-programs). As
expected, division by zero results in a runtime error and overflow by multiplication just
returns a wrong result, because the MIPS instruction doesn't have overflow detection. When
interpreting \texttt{zerodivision.pal} and \texttt{mult\_overflow.pal} we get the corresponding
MosML-exceptions, as expected.
\section{Type-checking of \texttt{read} and \texttt{new}}
%The idea is to use the ability to 'guess' the type of an expression by replacing \texttt{UnknownType} in calls to \texttt{typeCheckExp} with the acutal expected type whenever there is one. E.g. when checking an equality-expression we should use the checked type of the first expression as the expected type for the second, as the two should have the same type.

\subsection{Type-checking of \texttt{read}}
The return type of a \texttt{read()} call is determined by the context -- if it's assigned to some variable, for example, the return type is the type of that variable, or if it's used in an arithmethic expression, it must be an integer.

In order to determine this, we use the last parameter of \texttt{typeCheckExp}, the expected type. In any case where we know what the type of a sub-expression should be, we send that type along as a \texttt{KnownType}. For example, here's the updated code for type-checking a plus-expression:

\begin{lstlisting}
    | typeCheckExp(vtab, AbSyn.Plus (e1, e2, pos),_) =
        let val e1_new = typeCheckExp( vtab, e1, KnownType (BType Int))
            val e2_new = typeCheckExp( vtab, e2, KnownType (BType Int))
            val (tp1, tp2) = (typeOfExp e1_new, typeOfExp e2_new)
        in  if  typesEqual(BType Int, tp1) andalso typesEqual(BType Int, tp2)
            then Plus(e1_new, e2_new, pos)
            else raise Error(...)
        end
\end{lstlisting}

It's as simple as replacing each instance of \texttt{UnknownType} with \texttt{KnownType (BType Int)}.

Comparison operators (\texttt{=} and \texttt{<}) are slightly more complex -- here, we use the type of the first expression to determine the type of the second expression, like so:

\begin{lstlisting}
    | typeCheckExp (vtab,AbSyn.Equal(e1, e2, pos),_) =
        let val e1_new = typeCheckExp(vtab, e1, UnknownType)
            val e2_new = typeCheckExp(vtab, e2, KnownType (typeOfExp e1_new) )
            val (tp1, tp2) = (typeOfExp e1_new, typeOfExp e2_new)
            (* check that tp1 is not an array type *)
            val () = case tp1 of
                       Array _ => raise Error(...)
                     | _ => ()
        in  if  typesEqual(tp1, tp2)
            then Equal(e1_new, e2_new, pos)
            else raise Error(...)
        end
\end{lstlisting}

Here, the first expression still has an unknown type, and so cannot be a \texttt{read}-call.

\subsection{Type-checking of \texttt{new}}
The type-checking of \texttt{new} is done as was recommended in the comments in the code:

\begin{enumerate}
    \item Type-check all arguments with expected type \texttt{KnownType (BType Int)}
    \item Extract a list of all the types, using \texttt{map typeOfExp}.
    \item Check that all arguments are integers, and that there is at least one argument. If not, throw appropriate errors.
    \item If all of the above succeeds, return the type-ckecked FunApp.
\end{enumerate}

The code looks like this:
\begin{lstlisting}
  | typeCheckExp ( vtab, AbSyn.FunApp ("new", args, pos), etp ) =
  ( case expectedBasicType etp of
      SOME btp => let val new_args = map (fn a => typeCheckExp (vtab,a,KnownType (BType Int))) args
                      val arg_tps = map typeOfExp new_args
                  in if not (foldl (fn (t,b) => (b andalso
                             typesEqual(BType Int, t))) true arg_tps)
                     then raise Error(...)
                     else if length new_args > 0
                          then FunApp ( ("new", (arg_tps, SOME (Array(length new_args, btp))))
                                      , new_args, pos)
                          else raise Error(...)
                  end
    | NONE     => raise Error(...) )
\end{lstlisting}

In an ideal world, type-checking all arguments with integer as the expected type should be enough to ensure that they are all integers, but \texttt{typeCheckExp} only uses the expected type in cases where it's unclear what type an expression should have, such as \texttt{read}-calls. This means that \texttt{typeCheckExp(vtab, somePlusExpression, KnownType(BType Bool))} would not fail, even though a plus expression is always an integer type.

\section{Array indexing}
\subsection{Type checking}
Type checking of an array index is simply done by mapping a function that type checks an expression and raises an error if it is not of type \texttt{int} on the list of the given indexes. It also checks that the indexing is full, i.e. the rank of the array equals the length of the list of indexes.
The added clause to the function \texttt{typeCheckExp} is:
\begin{lstlisting}
    | typeCheckExp( vtab, AbSyn.LValue( AbSyn.Index(id, inds), pos ), _ ) =
       (case SymTab.lookup id vtab of
          NONE => raise Error("in type check indexed array, id "^id^" not in VTab, at ", pos)
        | SOME (tp as (Array (rank, e_type))) =>
          let val new_inds = map (fn exp =>
            let val new_i = typeCheckExp(vtab, exp, KnownType (BType Int))
            in  if typesEqual(BType Int, typeOfExp new_i)
                then new_i
                else raise Error("in type check one index in indexed array not"^
                                 "an int-expression, at ", pos)
            end inds
          in
            if rank > 0 andalso rank = length inds
            then LValue(Index((id, tp), new_inds), pos)
            else raise Error("in type check indexed array full indexing "^
                             "expected, at ", pos)
          end
        | _ => raise Error("Type check error: can only index "^
                           "into arrays, at ", pos)
       )
\end{lstlisting}
\subsection{Code generation}
The purpose of the machine code generated here is to calculate the address of a specific array element given the index (indices, if the array is multi-dimensional), the type of the elements and some metadata stored in the memory. At this point the array indexing has type-checked, so we can assume that the indexing is full and that all indices are actual integers. The calculation of the address will be performed in these steps:
\begin{enumerate}
  \item Firstly, we calculate the flat index.
  \item Then we find the base of the array, which is the last element in the metadata of the array.
  \item Finally, we add the flat index (multiplied by the byte-size of the type of the array elements, e.g. 4 for an int-array) and the base together to get the final address.
\end{enumerate}
Calculating the flat index we have not used the strides as this is not neccessary. To explain the method let's look at an example with a four dimensional array where the $k$'th dimension has the size $d_k$. The flat index $i_{flat}$ for the array position \texttt{a[i1,i2,i3,i4]} is
according to Section 3.2.3 calculated (with strides) like this:
\begin{equation*}
i_{flat} = i_1 s_1 + i_2 s_2 + i_3 s_3 + i_4 s_4
\end{equation*}
Using the recursive definition of the strides we get
\begin{align*}
i_{flat} = & 1*i_4 + d_4 i_3 + d_4 d_3 i_2 + d_4 d_3 d_2 i_1 \\
       =  & i_4 + d_4 (i_3 + d_3 (i_2 + (d_2 i_1))
\end{align*}
It should be quite clear now that the flat index can be calculated recursively by alternately
adding the next index $i_k$ and multiplying the result by $d_k+1$, i.e. if \texttt{t} is the
variable holding the flat index:
\begin{verbatim}
flatIndex:=0;
flatIndex:=t+i_1;
flatIndex:=t*d_2;
flatIndex:=t+i_2;
flatIndex:=t*d_3;
flatIndex:=t+i_3;
flatIndex:=t*d_4;
flatIndex:=t+i_4;
\end{verbatim}
\texttt{t} should now hold the flat index for \texttt{a[i1,i2,i3,i4]}.
It should also be clear that we can derive a similar method for any number of dimensions. We
will not this here, though. In \texttt{Compiler.sml} the flat index calculation is implemented
in the function \texttt{calcFIndex}:
\begin{lstlisting}
fun calcFIndex ([exp], dim) =
      let val index     = "_index"^newName()
          val dimSize = "_sizeOfDim"^newName()
          val compile_i = compileExp (vtab, exp, index)
          val getDimSize = [Mips.LW (dimSize, metadata_p, Int.toString (4*(dim-1)))]
      in  [Mips.ADDI (flatIndex, "0","0")] @ compile_i @ getDimSize
        @ (checkIndex index dimSize)
        @ [Mips.ADD (flatIndex, flatIndex, index)]
      end
  | calcFIndex (exp::exps, dim) =
      let val code1 = calcFIndex (exps, dim-1)
          val this_index = "_index"^newName()
          val dimSize = "_sizeOfDim"^newName()
          val compile_i = compileExp (vtab, exp, this_index)

          val getDimSize = [Mips.LW (dimSize, metadata_p, Int.toString (4*(dim-1)))]
          val code2 = [Mips.MUL (flatIndex, flatIndex, dimSize),
                       Mips.ADD (flatIndex, flatIndex, this_index)]
      in  code1
        @ compile_i @ getDimSize @ (checkIndex this_index dimSize)
        @ code2
      end
  | calcFIndex _ = raise Fail ("Should not be possible if LVal"^
                               "typechecks correctly")
\end{lstlisting}
Note, that the list of expressions is assumed to be reversed before given to \texttt{calcFIndex}.
For each index-expression. The expression is compiled and the size of the corresponding dimension
is loaded from the metadata. The index check \texttt{checkIndex} is added. I.e. the index check is
done as we go through the indices gradually calculating the flat index. (This way we only have to
load each dimension size once.) It should also be noted that the first clause (taking care of the
last index or the only index if the array is 1D) will always be reached and its MIPS-code will be
placed in the beginning of the instruction list. Therefore, it does make sense to instantiate the
variable \texttt{flatIndex} here. (Cf. \texttt{Mips.ADDI (flatIndex, "0", "0")}). The index checking
is done in \texttt{checkIndex}:
\begin{lstlisting}
fun checkIndex index dimSize =
              let val b1 = "_legalIndex"^newName()
                  val b2 = "_tmp"^newName()
              in
               [ Mips.SLT (b1, index, dimSize),
                 Mips.SLT (b2, index, "0"),
                 Mips.BEQ (b1, "0", "_IllegalArrIndexError_"),
                 Mips.BNE (b2, "0", "_IllegalArrIndexError_") ]
              end
\end{lstlisting}
It simply checks if a given index (integer) is non-negative and less than the given dimension size.
\newline
The rest of the \texttt{compileLVal}-clause for array indexing is shown below:
\begin{lstlisting}
    | compileLVal( vtab : VTab, Index ((n, Array(rank, elem_type)),inds) : LVAL, pos : Pos ) =
        (case SymTab.lookup n vtab of
          NONE => raise Error ("unknown variable " ^ n, pos)
        | SOME metadata_p =>
        let val base = "_base"^newName()
            val flatIndex = "_flatIndex"^newName()
            val addr = "_addr"^newName()

            (* Generate MIPS-code for checking if an array index is out of bounds *)
            fun checkIndex index dimSize =
              ...

            fun calcFIndex ([exp], dim) =
              ...

            (* ints are 4 bytes each and bools and chars are just 1 each *)
            val calcAddr = case elem_type of
              Int => [Mips.LW  (base, metadata_p, Int.toString (4*(rank*2-1))),
                      Mips.SLL (addr, flatIndex, "2"),
                      Mips.ADD (addr, addr, base)]
            | _   => [Mips.LW  (base, metadata_p, Int.toString (4*(rank *2-1))),
                      Mips.ADD (addr, flatIndex, base)]
            val fullcode = calcFIndex (rev inds, rank) @ calcAddr
        in
          (fullcode, Mem addr)
        end)
\end{lstlisting}
As mentioned above the last steps of the address calculation is to get the base-address (here
found by adding $4*(2n-1)$ ($n$ being the rank) to the metadata-pointer as there are $2n$
integers (of 4 bytes each) stored in a row. One of two different pieces of MIPS-code is generated
based on the type of elements in the array. If the array holds integer values we need to multiply
the flat index by four, i.e. shift the bits 2 times two the left. Note, that the list \texttt{inds}
of index-expressions is reversed as mentioned above. Lastly, the address is returned as a
\texttt{Location}-value (\texttt{Mem addr}).

\subsection{Tests of array indexing}
For this, there were already plenty of programs that would reveal errors in the implementation, such as \texttt{fibArray.pal} and \texttt{gameoflife.pal}. In addition, we wrote a few tests specifically for this. %TODO put in appendix and refer to that (those are long programs).

\section{Call-by-value-result semantics for procedures}
To implement call-by-value-result semantics, a slightly different procedure was necessary
for the interpreter and the compiler. 

\subsection{Interpretation of procedures}
In the interpreter the use of the SML references,
which are stored in the symbol table, allows for direct manipulation of the outer symbol table from inside the called function.
This is done by passing the old symbol table along as a parameter to callFun, which would use a copied symbol table for its inner statements.
This allowed us to simply update the references for those expressions, which we could identify as simply being a single identifier.
While it did require some pattern matching, to check the type of the expression, and extract the information the right way
from the symbol tables, it was relatively straigtforward.
% inner
\subsection{Compilation of procedures}
For the compiler, it was necessary to both change code for the caller and the callee.
The reason being, that the data is both copied into special transition registers by the caller and out of them by the callee.
The generated code of the callee starts by moving the bound parameters into transition registers. Our addition to the code
does exactly the opposite. After the code that makes up the body of the procedure, we add Mips.MOVE-instructions
to move the values, not from \texttt{reg} to \texttt{vname}, but from \texttt{vname} to \texttt{reg}.

We have added our code to the list of instructions given to the register allocater, because it uses the same
registers as \texttt{argcode} -- When the symbolic registers in \texttt{argcode} are assigned, we want it to
use the same registers for moving the other way.
% outer
\\
For the caller, we use that the constuctor for the code that allocates data to the transition registers,
and simply let it construct an equivalent piece of code for each part that moves them back.
This is however not completely trivial, since those sources themselves are actually temporary registers
for the results of calculated the expressions. So instead, we check to see if each expression is equivalent
to a single identifier, for which we then look up the register for that expression in the symbol table,
and use that, and if an expresions is not on that form, we simply do not add any code to move such a register back.
Since this code needs to be put after the function call, then the constructer returns this move back code as a seperate
return value (of the now triple turple), and that code is then appended to code for calling the procedure
(meaning after the jump to the procedure).

\newpage
\begin{appendix}
\section{Test framework}
We wrote a small test framework in Python and \texttt{sh} for automatic testing. It only works on UNIX environments, and
requires Python 2.x.

The framework can be found in the \texttt{DATA} folder,
and can be run with the command:

\begin{verbatim}
$ python test.py
\end{verbatim}
This will output the various tests, grouped by the feature they are testing (for example, there is a group called
``if'' and one called ``arrays''). It is also possible to run select testgroups with the command:

\begin{verbatim}
$ python test.py <groups>
\end{verbatim}
For example, the following command runs all tests related to if-statements and arrays:

\begin{verbatim}
$ python test.py if arrays
\end{verbatim}

The following command print a list of all test groups:

\begin{verbatim}
$ python test.py -l
\end{verbatim}

The source code for the test framework can be found in the files \texttt{test.py} and \texttt{marsrun}.

\subsection{test.py}
Test framework for Paladim
\lstinputlisting[language=python]{../DATA/test.py}
\subsection{marsrun}
\texttt{marsrun} simply compiles a .pal file, runs the resulting .asm file, and then deletes it. It takes one
argument, which is the filename, but without the .pal extension.
\lstinputlisting[language=sh]{../DATA/marsrun}

\section{Source code}
\subsection{Parser.grm}
    \lstinputlisting[language=Yacc, style = appendix]{../SRC/Parser.grm}

\subsection{ParserTest.sml}
    \lstinputlisting[language=SML, style = appendix]{../SRC/ParserTest.sml}

\subsection{Compiler.sml}

All the changes and additions to Compiler.sml.

\subsubsection{\texttt{or} and \texttt{not}}

\Cfile{Compiler.sml}{253}{270}
%    \lstinputlisting[language=SML, style = appendix,firstline=253,lastline=270]{../SRC/Compiler.sml}
\subsubsection{Indexes in arrays}
\Cfile{Compiler.sml}{465}{521}
%    \lstinputlisting[language=SML, style = appendix,firstline=465,lastline=521]{../SRC/Compiler.sml}
\subsubsection{Call-by-value-result semantics for procedures}
\Cfile{Compiler.sml}{433}{454}
%    \lstinputlisting[language=SML, style = appendix,firstline=433,lastline=454]{../SRC/Compiler.sml}
\Cfile{Compiler.sml}{574}{582}
%    \lstinputlisting[language=SML, style = appendix,firstline=574,lastline=582]{../SRC/Compiler.sml}
\Cfile{Compiler.sml}{652}{692}
%    \lstinputlisting[language=SML, style = appendix,firstline=652,lastline=692]{../SRC/Compiler.sml}

\subsection{Type.sml}
\subsubsection{Type-checking array indexes}
\Cfile{Type.sml}{159}{177}
%    \lstinputlisting[language=SML, style = appendix,firstline=159,lastline=177]{../SRC/Type.sml}
\subsubsection{Type inference for \texttt{read}}
Most instances of \texttt{UnknownType} have been changed into \texttt{KnownType}s.
\Cfile{Type.sml}{179}{283}
%    \lstinputlisting[language=SML, style = appendix,firstline=179,lastline=283]{../SRC/Type.sml}
\subsubsection{Type-checking \texttt{new}}
\Cfile{Type.sml}{316}{333}
%    \lstinputlisting[language=SML, style = appendix,firstline=316,lastline=333]{../SRC/Type.sml}
\subsubsection{Type-checking \texttt{or}, \texttt{not}, \texttt{*} and \texttt{/}}
\Cfile{Type.sml}{201}{220}
%    \lstinputlisting[language=SML, style = appendix,firstline=201,lastline=220]{../SRC/Type.sml}
\Cfile{Type.sml}{265}{283}
%    \lstinputlisting[language=SML, style = appendix,firstline=265,lastline=283]{../SRC/Type.sml}

\subsection{TpInterpret.sml}
\Cfile{TpInterpret.sml}{121}{129}
%    \lstinputlisting[language=SML, style = appendix,firstline=121,lastline=129]{../SRC/TpInterpret.sml}
\Cfile{TpInterpret.sml}{494}{507}
%    \lstinputlisting[language=SML, style = appendix,firstline=494,lastline=507]{../SRC/TpInterpret.sml}
\Cfile{TpInterpret.sml}{527}{534}
%    \lstinputlisting[language=SML, style = appendix,firstline=527,lastline=534]{../SRC/TpInterpret.sml}
\Cfile{TpInterpret.sml}{163}{183}
%    \lstinputlisting[language=SML, style = appendix,firstline=163,lastline=183]{../SRC/TpAbSyn.sml}
\end{appendix}
\end{document}
